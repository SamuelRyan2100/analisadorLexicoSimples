<programa> ::= <comando_lista>

<comando_lista> ::= <comando> <comando_lista> | ε

<comando> ::= <atribuicao> ";"
             | <se>
             | <enquanto>
             | "{" <comando_lista> "}"

<atribuicao> ::= <identificador> "=" <expressao>

<se> ::= "if" "(" <expressao> ")" <comando> <senao_opcional>

<senao_opcional> ::= "else" <comando> | ε

<enquanto> ::= "while" "(" <expressao> ")" <comando>

<expressao> ::= <expressao_logica>

<expressao_logica> ::= <expressao_relacional> <op_logico> <expressao_logica>
                     | <expressao_relacional>

<op_logico> ::= "&&" | "||"

<expressao_relacional> ::= <expressao_aritmetica> <op_relacional> <expressao_aritmetica>
                         | <expressao_aritmetica>

<op_relacional> ::= "==" | "!=" | ">" | "<" | ">=" | "<="

<expressao_aritmetica> ::= <termo> { ("+" | "-") <termo> }

<termo> ::= <fator> { ("*" | "/") <fator> }

<fator> ::= <numero>
           | <booleano>
           | <identificador>
           | "(" <expressao> ")"

<booleano> ::= "true" | "false"

<numero> ::= <NUMERO_INT> | <NUMERO_REAL>

<identificador> ::= <IDENTIFICADOR>
